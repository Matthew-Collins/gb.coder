' Gambas class file

Inherits TextEditor

Public ProjectFile As ProjectFile
Public ProjectFileObserver As Observer

Public MeObserver As New Observer(Me, True) As "Me"

Public Sub _new(_ProjectFile As ProjectFile)

  With Me
    .ProjectFile = _ProjectFile
    .ProjectFileObserver = New Observer(.ProjectFile, True) As "ProjectFileObserver"

    Select Case .ProjectFile.Type
      Case ProjectTypes.Gambas, ProjectTypes.GambasScript, ProjectTypes.Vb, ProjectTypes.VbCore
        .Highlight = "Gambas"
      Case ProjectTypes.Swift, ProjectTypes.SwiftScript, ProjectTypes.SwiftPackage
        .Highlight = "C"
      Case ProjectTypes.Css
        .Highlight = "CSS"
      Case ProjectTypes.Html
        .Highlight = "HTML"
    End Select

    .Text = .ProjectFile.LoadText()

    .Font.Name = "Courier 10 Pitch"
    .Font.Size = 9

    .ShowLineNumber = True
    .ShowIcon = True
    .ShowIndent = True
    .ShowCurrent = True
    .ShowPreview = True
    .ShowBraces = True

    .Breakpoints = ProjectFile.Breakpoints

  End With

End

Public Sub Me_Margin()

  Me.Current.Breakpoint = Not Me.Current.Breakpoint
  Me.ProjectFile.Breakpoints = Me.Breakpoints
  Me.ProjectFile.Project.UpdateBreakpoints()

End

Public Function OffSet() As Integer

  Dim y As Integer
  Dim Count As Integer

  For y = 0 To Me.Line - 1
    Count += Me[y].Length + 1
  Next
  Count += Me.Column

  Return Count

End

Public Sub Me_KeyPress()

  If Key.code = Key.Up Then
    If Me.ProjectFile.Project.NavigateCodeCompletion(-1) Then
      Stop Event
    Endif
  Else If Key.code = Key.Down Then
    If Me.ProjectFile.Project.NavigateCodeCompletion(1) Then
      Stop Event
    Endif
  End If

End

Public Sub Me_KeyRelease()

  If Key.Code = Key.Esc Then
    Me.ProjectFile.Project.PulldownCodeCompletion(False)
  Else If Key.code = Key.Up Then
    If Me.ProjectFile.Project.NavigateCodeCompletion(0) Then
      Stop Event
    Endif
  Else If Key.code = Key.Down Then
    If Me.ProjectFile.Project.NavigateCodeCompletion(0) Then
      Stop Event
    Endif
  Else
    Me.ProjectFile.Project.PopupCodeCompletion(Me)
  Endif

End

Public Sub Me_MouseDown()

  Me.ProjectFile.Project.PulldownCodeCompletion(False)

End

Public Sub ProjectFileObserver_Stopped(LineNumber As Integer)

  Me.StopLine = LineNumber - 1

End

Public Sub ProjectFileObserver_FlushChanges()

  Flush()

End

Public Sub Flush()

  Me.ProjectFile.SaveText(Me.Text)

End
