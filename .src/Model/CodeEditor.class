' Gambas class file

Inherits TextEditor

Public ProjectFile As ProjectFile
Public ProjectFileObserver As Observer

Public MeObserver As New Observer(Me, True) As "MeObserver"

Public Sub _new(_ProjectFile As ProjectFile)

  With Me
    .ProjectFile = _ProjectFile
    .ProjectFileObserver = New Observer(.ProjectFile, True) As "ProjectFileObserver"

    Select Case .ProjectFile.Type
      Case ProjectTypes.Gambas, ProjectTypes.GambasScript, ProjectTypes.Vb, ProjectTypes.VbCore
        .Highlight = "gambas"
      Case ProjectTypes.Swift, ProjectTypes.SwiftScript
        .Highlight = "c"
    End Select

    .Text = .ProjectFile.LoadText()

    .Font.Name = "Courier 10 Pitch"
    .Font.Size = 9

    .ShowLineNumber = True
    .ShowIcon = True
    .ShowIndent = True
    .ShowCurrent = True

    .Breakpoints = ProjectFile.Breakpoints

  End With

End

Public Sub MeObserver_MouseDown()

  If Mouse.x > 16 And Mouse.x < 34
    Me.Current.Breakpoint = Not Me.Current.Breakpoint
    Me.ProjectFile.Breakpoints = Me.Breakpoints
    Me.ProjectFile.Project.UpdateBreakpoints()
  End If

End

Public Sub ProjectFileObserver_Stopped(LineNumber As Integer)

  Me.StopLine = LineNumber - 1

End

Public Sub ProjectFileObserver_FlushChanges()

  Flush()

End

Public Sub Flush()

  Me.ProjectFile.SaveText(Me.Text)

End
