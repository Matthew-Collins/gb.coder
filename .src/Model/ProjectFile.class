' Gambas class file

Public Project As Project
Public Folders As String
Public FileName As String
Public Type As Integer

Public Breakpoints As New Integer[]

Event FlushChanges
Event Stopped(LineNumber As Integer)

Property Read FullFileName As String

Public Sub _new(_Project As Project, _Folders As String, _FileName As String, _Type As Integer)

  Me.Project = _Project
  Me.Folders = _Folders
  Me.FileName = _FileName
  Me.Type = _Type

End

Function FullFileName_Read() As String

  Return Me.Project.RootFolder &/ Me.Folders &/ Me.FileName

End

Public Sub AddTab(Tabs As TabPanel, UseLastTab As Boolean)

  If Not UseLastTab Then
    Tabs.Count += 1
  Endif

  GetEditor(Tabs[Tabs.Count - 1])
  With Tabs[Tabs.Count - 1]
    .Text = FileName
    .Tag = Me
  End With

End

Public Function GetEditor(Container As Container) As CodeEditor

  Return New CodeEditor(Container, Me)

End

Public Function LoadText() As String

  Dim Text As String = ""

  If Exist(Me.FullFileName) Then
    Text = File.Load(Me.FullFileName)
  Endif

  Return Text

End

Public Function SaveText(Text As String)

  File.Save(Me.FullFileName, Text)

End

Public Sub Flush()

  Raise FlushChanges

End

Public Sub StoppedAt(LineNumber As Integer)

  Raise Stopped(LineNumber)

End
